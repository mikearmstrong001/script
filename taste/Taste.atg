#include "ast.h"
#include "wchar.h"

COMPILER Taste

	int // operators
	  plus, minus, times, slash, equ, lss, gtr;

	int // types
	  undef, integer, boolean, floatingpoint, voidtype, userptr, stringtype;

	int // object kinds
	  var, proc;
	
	Package		  *package;

	void Err(wchar_t* msg) {
		errors->Error(la->line, la->col, msg);
	}

	void InitDeclarations() { // it must exist
		plus = 0; minus = 1; times = 2; slash = 3; equ = 4; lss = 5; gtr = 6; // operators
		undef = 0; boolean = 1; integer = 2; floatingpoint = 3; voidtype = 4; userptr = 5; stringtype=6; // types
		var = 0; proc = 1; // object kinds
		package = NULL;
	}

	bool IsMemberFunc()
	{
		Token *next0 = scanner->Peek();
		Token *next1 = scanner->Peek();
		return la->val[0] == '<' && next1->val[0] == '>';
	}

	bool IsFunction()
	{
		if ( la->val[0] == '<' )
		{
			Token *next0 = scanner->Peek();
			Token *next1 = scanner->Peek();
			if ( next1->val[0] != '>' )
				return false;
			Token *next2 = scanner->Peek();
			return next2->val[0] == '(';
		}
		else
		{
			return la->val[0] == '(';
		}
	}

  
/*--------------------------------------------------------------------------*/
CHARACTERS
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  character = ANY - '"' - '\\' - '\n' - '\r'.
  digit = "0123456789".
  digitnozero = "123456789".
  zero = "0".
  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.

TOKENS
  ident  = [ letter {letter | digit | '_' } ':' ] letter {letter | digit | '_' }.
  //varname = [ letter {letter | digit} ':' ] letter {letter | digit}.
  number = digit {digit}.
  floatingpoint = digit {digit} '.' {digit}.
  characters = '"' {character} '"'.
  
COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO cr lf

IGNORE cr + lf + tab



PRODUCTIONS
/*------------------------------------------------------------------------*/

Type<int &type>
=
 ( "int"						(. type = integer; .)
 | "bool"						(. type = boolean; .)
 | "float"						(. type = floatingpoint; .)
 | "userptr"					(. type = userptr; .)
 | "string"						(. type = stringtype; .)
 | ident						(. type = Hash( t->val ); .)
 ).

/*------------------------------------------------------------------------*/
ReturnType<int &type>
= 
	"void"			(. type = voidtype; .)
	| Type<type>
  .

/*------------------------------------------------------------------------*/


VarDecl<VarDeclAst* &varDecl>	(. int type; varDecl = new VarDeclAst; .)
= 
	"var"
	(
		(
			Type<type>				(. varDecl->SetType(type); .)
			( 
				[ 
					'[' ']'					(. varDecl->SetIsArray(); .)
				]
			)
			ident							(. varDecl->SetName(t->val); .) 
			';'
		)
	)
	.

/*------------------------------------------------------------------------*/
AddOp<int &op>
=
	( 
		'+'				(. op = plus; .)
		| 
		'-'				(. op = minus; .)
	)
	.

/*------------------------------------------------------------------------*/
MulOp<int &op>
=
	( 
		'*'				(. op = times; .)
		| 
		'/'				(. op = slash; .)
	)
	.

/*------------------------------------------------------------------------*/
Expr<AstBase* &expr>			(. AstBase* other_expr; int op; .)
= 
	SimExpr<other_expr>		(. expr = other_expr; .)
	[ 
		RelOp<op>
		SimExpr<other_expr>		(. expr = new BinaryAst( expr, op, other_expr ); .)
	]
	.

/*------------------------------------------------------------------------*/
ObjectMemberList<IdentVec &vec>
=
	ident						(. vec.push_back( t->val ); .)
	{
		'.'
		ObjectMemberList<vec>
	}
	.
/*------------------------------------------------------------------------*/
Factor<AstBase* &factor>			(. AstBase *other_factor; .)
=                        
	(
		ident						(. IdentAst *identFactor = new IdentAst( t->val ); factor = identFactor; IdentVec identVec; AstVec exprVec; AstBase *expr; .)
		[
			( 
				'[' 
					Expr<expr>		(. identFactor->SetArrayIndex( expr ); .)
				']'		
			)
			| 
			(
				'.' ObjectMemberList<identVec>	(. identFactor->SetMemberList( identVec ); .)
			)
			| 
			(
			[ IF(IsFunction())
				[
					'<'
					ident
					'>'
				]
				"("
					[ 
						ExprList<exprVec>	
					]
				")"					(. factor = new CallAst( identFactor->GetNameWC(), identVec, exprVec ); delete identFactor; .)
			]
			)
		]
	)
	|
	( 
		number						(. factor = new ConstIntegerAst( t->val ); .)
	)
	| 
	(
		floatingpoint				(. factor = new ConstFloatingPointAst( t->val ); .)
	)
	| 
	(
		characters					(. factor = new ConstStringAst( t->val ); .)
	)
	| 
	( 
		'-' Factor<other_factor>	(. factor = new NegateAst( other_factor ); .)
	)
	| 
	(
		"true"						(. factor = new ConstBooleanAst( true ); .)
	)
	| 
	(	
		"false"						(. factor = new ConstBooleanAst( false ); .)
	)
	| 
	(
		'(' 
			Expr<factor> 
		')'
	)
	.
/*------------------------------------------------------------------------*/

Declaration<DeclInfo &decl>		
= 
	Type<decl.type>				
	ident							(. decl.name = t->val; .)
	.

DeclarationList<DeclVec &vec>	(. DeclInfo decl; .)
=
	Declaration<decl>			(. vec.push_back( decl ); .)
	{
		","
		Declaration<decl>		(. vec.push_back( decl ); .)
	}
	.

ExprList<AstVec &vec>			(. AstBase *expr; .)
=
	Expr<expr>					(. vec.push_back( expr ); .)
	{ 
		',' 
		Expr<expr>				(. vec.push_back( expr ); .)
	}.
/*------------------------------------------------------------------------*/
ProcDecl<ProcDeclAst* &procDecl>		(. VarDeclAst *varDecl; int type; AstBase *stat; DeclVec declVec; .)
= 
	"function"							(. procDecl = new ProcDeclAst(); .)
	ReturnType<type>						(. procDecl->SetReturnType( type ); .)
	ident									(. procDecl->SetName( t->val ); .)
	[
		'<'
		ident
		ident
		'>'
	]
	'('
		[ 
			DeclarationList<declVec>
		]
	')'									(. procDecl->SetDeclaration(declVec); .)
	'{'                    
	{ 
		VarDecl<varDecl>				(. procDecl->AddBody( varDecl ); .)
		| 
		Stat<stat>						(. procDecl->AddBody( stat ); .)
	}
	'}'
	.

/*------------------------------------------------------------------------*/
RelOp<int &op>
=
	( 
		"=="						(. op = equ; .)
		| '<'							(. op = lss; .)
		| '>'							(. op = gtr; .)
	)
	.

/*------------------------------------------------------------------------*/
SimExpr<AstBase* &expr>			(. int op; AstBase *term; .)
= 
	Term<term>					(. expr = term; .)
	{ 
		AddOp<op>
		Term<term>					(. expr = new BinaryAst( expr, op, term ); .)
	}
	.
/*------------------------------------------------------------------------*/
Term<AstBase* &term>		(. int op; AstBase *factor; .)
= 
	Factor<factor>			(. term = factor; .)
	{ 
		MulOp<op>
		Factor<factor>			(. term = new BinaryAst( term, op, factor ); .)
	}
	.
/*------------------------------------------------------------------------*/
Stat<AstBase *&stat>			(. VarDeclAst *varDecl; AstBase *expr = NULL; AstBase *substat; .)
= 
	(
		"if"						
		'(' 
			Expr<expr> 
		')'						(. IfAst *ifstat = new IfAst( expr ); stat = ifstat; .)
		'{' 
			{ 
				Stat<substat>				(. ifstat->AddIf( substat ); .)
				| VarDecl<varDecl>		(. ifstat->AddIf( varDecl ); .)
			} 
		'}'	
		[ 
			"else"
			'{' 
				{ 
					Stat<substat>				(. ifstat->AddElse( substat ); .)
					| VarDecl<varDecl>			(. ifstat->AddElse( varDecl ); .)
				} 
			'}'
		]
	)
	| 
	(
		"return"
		[ 
			Expr<expr> 
		]
		';'							(. stat = new ReturnStat( expr ); .)
	)
	| 
	(
		"while"						    
		'(' 
			Expr<expr> 
		')'			(. WhileAst *whilestat = new WhileAst( expr ); stat = whilestat; .)
		'{' 
			{ 
				Stat<substat>				(. whilestat->AddExpr( substat ); .)
				| VarDecl<varDecl>			(. whilestat->AddExpr( varDecl ); .)
			} 
		'}'
	) 						
	| 
	(
		'{'							(. BlockAst *blockstat = new BlockAst; stat = blockstat; .)
			{ 
				Stat<substat>				(. blockstat->AddExpr( substat ); .)
				| VarDecl<varDecl>			(. blockstat->AddExpr( varDecl ); .)
			} 
		'}'
	)
	| 
	(
		ident							(. IdentAst *identStat = new IdentAst( t->val ); stat = identStat; IdentVec identVec; AstBase *expr; .)
		(
			[
				'['
				Expr<expr>					(. identStat->SetArrayIndex( expr ); .)
				']'				
			]
			[
				'.' 
				ObjectMemberList<identVec> (. identStat->SetMemberList( identVec ); .)
			]			
			[
				( 
					'='
					Expr<expr>			(. stat = new AssignAst( identStat->GetNameWC(), identStat->GetArrayIndex(), identVec, expr ); delete identStat; .)
				)
				| 
				(
					[
						IF(IsMemberFunc())
						'<'
						ident
						'>'
					]
					'('							(. AstVec exprVec; .)
						[
							ExprList<exprVec>
						]
					')'						(. stat = new CallAst( identStat->GetNameWC(), identVec, exprVec ); delete identStat; .)
				)
			]
		)
		';'
	)
	.


Struct<StructDecl *&def>				(. VarDeclAst *varDecl; .)
=
	"struct"
	ident							(. def = new StructDecl( t->val ); .)
	'{'
		{
			VarDecl<varDecl>		(. def->AddVarDecl( varDecl ); .)
		}
	'}'
	';'
	.


ProcDefDecl<ProcDefDeclAst* &procDecl>		(. int type; DeclVec declVec; .)
= 
	"function"							(. procDecl = new ProcDefDeclAst(); .)
	ReturnType<type>						(. procDecl->SetReturnType( type ); .)
	ident									(. procDecl->SetName( t->val ); .)
	'('
		[ 
			DeclarationList<declVec>
		]
	')'									(. procDecl->SetDeclaration(declVec); .)
	';'
	.


Interface<InterfaceDecl *&def>				(. ProcDefDeclAst *procDecl; .)
=
	"interface"
	ident							(. def = new InterfaceDecl( t->val ); .)
	'{'
		{
			ProcDefDecl<procDecl>		(. def->AddProcDecl( procDecl ); .)
		}
	'}'
	';'
	.

/*------------------------------------------------------------------------*/
Taste			(. StructDecl *defDecl; VarDeclAst *varDecl; ProcDeclAst *procDecl; InterfaceDecl *ifaceDecl; .)
= "package"		
  ident			(. package = new Package( t->val ); .)
  '{'
	  { 
		Interface<ifaceDecl>  (. package->AddInterfaceDecl( ifaceDecl ); .)
		| Struct<defDecl>  (. package->AddStructDecl( defDecl ); .)
		| VarDecl<varDecl>		(. package->AddVarDecl( varDecl ); .)
		| ProcDecl<procDecl>	(. package->AddProcDecl( procDecl ); .)
	  }
  '}'
  .
/*------------------------------------------------------------------------*/

END Taste.
